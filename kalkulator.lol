HAI 1.4

	BTW special characters sadly not supported

	I HAS A JEDEN
	I HAS A DWA 
	I HAS A EX
	I HAS A LNG
	I HAS A DEFAULT
	I HAS A LNGS ITZ A BUKKIT
	I HAS A ZWROTY ITZ A BUKKIT
	BTW English
	LNGS HAS A SRS 1 ITZ "English"
	DEFAULT R 1
	ZWROTY HAS A SRS 1 ITZ A BUKKIT
	ZWROTY'Z SRS 1 HAS A choose ITZ "Choose language."
	ZWROTY'Z SRS 1 HAS A first ITZ ":)Welcome in calculator. :)Input first number:"
	ZWROTY'Z SRS 1 HAS A second ITZ " :)Input second number:"
	ZWROTY'Z SRS 1 HAS A opnodiv ITZ ":)Choose an operation: :)1. Add :)2. Subtract :)3. Multiply"
	ZWROTY'Z SRS 1 HAS A op ITZ "Choose an operation: :)1. Add :)2. Subtract :)3. Multiply :)4. Divide"
	ZWROTY'Z SRS 1 HAS A error ITZ "Choose the operation through inputing a corresponding number."
	ZWROTY'Z SRS 1 HAS A langerror ITZ "Those are only supported languages rn. Feel free to add translations."
	BTW polski
	LNGS HAS A SRS 2 ITZ "polski"
	ZWROTY HAS A SRS 2 ITZ A BUKKIT
	ZWROTY'Z SRS 2 HAS A choose ITZ "Choose language."
	ZWROTY'Z SRS 2 HAS A first ITZ ":)Witaj w kalkulatorze. :)Wprowadz pierwsza liczbe:"
	ZWROTY'Z SRS 2 HAS A second ITZ " :)Wprowadz druga liczbe:"
	ZWROTY'Z SRS 2 HAS A opnodiv ITZ ":)Wybierz dzialanie: :)1. Dodawanie :)2. Odejmowanie :)3. Mnozenie"
	ZWROTY'Z SRS 2 HAS A op ITZ "Wybierz dzialanie: :)1. Dodawanie :)2. Odejmowanie :)3. Mnozenie :)4. Dzielenie"
	ZWROTY'Z SRS 2 HAS A error ITZ "Wybierz dzialanie poprzez wpisanie odpowiedniego numeru."
	ZWROTY'Z SRS 2 HAS A langerror ITZ "Those are only supported languages rn. Feel free to add translations."
	I HAS A CURRENT ITZ ZWROTY'Z SRS DEFAULT
	VISIBLE CURRENT'Z choose
	IM IN YR lngs UPPIN YR INDEX TIL BOTH SAEM INDEX AN 2
		VISIBLE SMOOSH SUM OF INDEX AN 1 AN ". " AN LNGS'Z SRS SUM OF INDEX AN 1 MKAY
	IM OUTTA YR lngs
	GIMMEH LNG
	LNG IS NOW A NUMBR
	BOTH OF BOTH SAEM LNG AN BIGGR OF 1 AN LNG AN BOTH SAEM LNG AN SMALLR OF 2 AN LNG
	O RLY?
		YA RLY
			CURRENT R ZWROTY'Z SRS LNG
			VISIBLE CURRENT'Z first
			GIMMEH JEDEN
			VISIBLE CURRENT'Z second
				GIMMEH DWA
			JEDEN IS NOW A NUMBAR
			DWA IS NOW A NUMBAR
			BOTH SAEM DWA AN 0.0, O RLY?
				YA RLY
					VISIBLE CURRENT'Z opnodiv
					GIMMEH EX
					EX, WTF?
						OMG "1"
							VISIBLE SUM OF JEDEN AN DWA
							GTFO
						OMG "2"
							VISIBLE DIFF OF JEDEN AN DWA
							GTFO
						OMG "3"
							VISIBLE PRODUKT OF JEDEN AN DWA
							GTFO
						OMGWTF
							VISIBLE CURRENT'Z error
							GTFO
						OIC
				NO WAI
					VISIBLE CURRENT'Z op
					GIMMEH EX
					EX, WTF?
						OMG "1"
							VISIBLE SUM OF JEDEN AN DWA
							GTFO
						OMG "2"
							VISIBLE DIFF OF JEDEN AN DWA
							GTFO
						OMG "3"
							VISIBLE PRODUKT OF JEDEN AN DWA
							GTFO
						OMG "4"
							VISIBLE QUOSHUNT OF JEDEN AN DWA
							GTFO
						OMGWTF
							VISIBLE CURRENT'Z error
							GTFO
						OIC
			OIC
		NO WAI
			VISIBLE CURRENT'Z langerror
	OIC
KTHXBYE
